# RungeKutta uses the common unpacking, reference-ramp, alignment, aperture, and
# callback path. Only the body field integration is specific to RungeKutta.

@inline function runge_kutta_multipoles(bmultipoleparams, L, p_over_q_ref)
  if !isactive(bmultipoleparams)
    return SVector{0,Int}(), SVector{0,typeof(L)}(), SVector{0,typeof(L)}()
  end

  mm = getfield(bmultipoleparams, :order)
  kn, ks = get_strengths(bmultipoleparams, L, p_over_q_ref)
  if mm isa Integer
    return SA[mm], SA[kn], SA[ks]
  end
  return mm, kn, ks
end

@inline function runge_kutta_body(
  tm::RungeKutta,
  kc,
  p_over_q_ref,
  bunch,
  bendparams,
  bmultipoleparams,
  patchparams,
  rfparams,
  mapparams,
  fourpotentialparams,
  emultipoleparams,
  L,
)
  L > 0 || error("RungeKutta tracking requires a positive element length")
  !isactive(patchparams) || error("RungeKutta tracking does not support patch elements")
  !isactive(rfparams) || error("RungeKutta tracking does not support RF fields")
  !isactive(mapparams) || error("RungeKutta tracking does not support map elements")
  !isactive(fourpotentialparams) || error("RungeKutta tracking does not support FourPotentialParams")
  !isactive(emultipoleparams) || error("RungeKutta tracking does not support electric multipoles")

  if isactive(bendparams)
    (bendparams.edge1_int == 0 && bendparams.edge2_int == 0) ||
      error("edge1_int and edge2_int not yet handled for tracking")
    (bendparams.e1 == 0 && bendparams.e2 == 0) ||
      error("RungeKutta tracking does not support nonzero bend edge angles e1 or e2 because fringe tracking is not implemented")
    g_ref = bendparams.g_ref
    tilt_ref = bendparams.tilt_ref
    gx = g_ref * cos(tilt_ref)
    gy = g_ref * sin(tilt_ref)
  else
    gx = zero(L)
    gy = zero(L)
  end

  species = bunch.species
  tilde_m, _, beta_0 = BeamTracking.drift_params(species, p_over_q_ref)
  charge = chargeof(species)
  p0c = BeamTracking.R_to_pc(species, p_over_q_ref)
  mc2 = massof(species)
  n_steps, ds_step = BeamTracking.find_steps(tm, L)
  mm, kn, ks = runge_kutta_multipoles(bmultipoleparams, L, p_over_q_ref)

  # Time-dependent values in params are evaluated once, at the particle's
  # element-entrance time, by the common kernel path. They stay fixed during
  # all RK substeps.
  field = BeamTracking.RungeKuttaTracking.MultipoleSource(
    mm, kn, ks, p_over_q_ref, gx, gy,
  )
  params = (beta_0, tilde_m, charge, p0c, mc2, L, ds_step, n_steps, field)
  return push(kc, make_kernel_call(BeamTracking.RungeKuttaTracking.rk4_kernel!, params))
end
